buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.gorylenko.gradle-git-properties:gradle-git-properties:2.4.1"
    }
}


description = "WaldOT API"
group = 'net.rossonet.waldot'
version = "${rootProject.version}"

ext {
    sourceCompatibility = 21
    targetCompatibility = 21
    ossrhPassword = System.getenv('OSSRH_PASSWORD')
    ossrhUsername = System.getenv('OSSRH_USERNAME')
}

repositories {
    mavenCentral()
    maven {
       url "https://repo.craftyn.com/repository/snapshots/" 
    }
    maven { url 'https://jitpack.io' }
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: "com.gorylenko.gradle-git-properties"
apply plugin: 'jacoco'
apply plugin: 'distribution'
apply plugin: 'signing'
apply plugin: 'maven-publish'

dependencies {
    // gremlin
    implementation group: 'org.apache.tinkerpop', name: 'gremlin-core', version: '3.7.3'
    implementation 'org.json:json:20250107'
    implementation 'org.yaml:snakeyaml:2.4'
    implementation 'org.apache.commons:commons-jexl3:3.4.0'
    implementation 'org.slf4j:jul-to-slf4j:2.0.17'
    implementation 'org.slf4j:slf4j-simple:2.0.16'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.18.2'
    // OPCUA
    implementation group: 'org.eclipse.milo', name: 'sdk-server', version: '0.6.15'
    implementation group: 'org.eclipse.milo', name: 'dictionary-manager', version: '0.6.15'
    implementation group: 'org.eclipse.milo', name: 'dictionary-reader', version: '0.6.15'
    implementation group: 'org.eclipse.milo', name: 'bsd-parser-core', version: '0.3.8'

    annotationProcessor group: 'org.apache.tinkerpop', name: 'gremlin-annotations', version: '3.7.3'
    //implementation 'ch.qos.logback:logback-classic:1.5.8'
    //implementation group: 'info.picocli', name: 'picocli', version: '4.7.6'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.12.0'
    testImplementation group: 'com.ginsberg', name: 'junit5-system-exit', version: '2.0.2'
    testImplementation group: 'com.google.inject', name: 'guice', version: '7.0.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.12.0'
}

task eclipseClosedDependencies {}

task eclipseClosedDependencies_ {}

compileJava {
    options.encoding = "UTF-8"
    options.compilerArgs.addAll(['-XprintProcessorInfo'])
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

task view { doLast { println "Working on project ${project.name} [ ${project.description} ]" } }

jar {
    enabled = true
}

jar.doLast { task ->
	ant.checksum file: task.archivePath
}

java {
    withJavadocJar()
    withSourcesJar()
}

gitProperties {
    failOnNoGitDirectory = false
    customProperty 'component', "${project.name}"
    dateFormat = "yyyy-MM-dd HH:mm:ssZ"
    dateFormatTimeZone = 'GMT'
}

test {
    useJUnitPlatform()
}

javadoc{
    destinationDir = file("${project.buildDir}/docs/javadoc/${project.name}/")
    failOnError=false
}

publishing {
  publications {
    mavenJava(MavenPublication){
      artifactId = "${project.name}"
      from components.java
      versionMapping {
        usage('java-api') {
          fromResolutionOf('runtimeClasspath')
        }
        usage('java-runtime') {
          fromResolutionResult()
        }
      }
      pom {
        name = "${project.name}"
        description = "${project.description}"
        url = 'https://www.rossonet.net/'
        licenses {
          license {
            name = 'Apache-2.0 license'
            url = 'https://www.apache.org/licenses/LICENSE-2.0'
          }
        }
        developers {
          developer {
            id = 'ar4k'
            name = 'Andrea Ambrosini'
            email = 'andrea.ambrosini@rossonet.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/rossonet/waldot.git'
          developerConnection = 'scm:git:ssh://github.com:rossonet/waldot.git'
          url = 'https://github.com/rossonet/waldot/tree/master'
        }
      }
    }
  }
  repositories {
    maven {
      url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      name = "sonatype"
      credentials {
        username "${project.ext.ossrhUsername}"
        password "${project.ext.ossrhPassword}"
      }
    }
  }
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

signing {
    sign publishing.publications.mavenJava
}
