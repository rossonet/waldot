buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.gorylenko.gradle-git-properties:gradle-git-properties:2.4.1"
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:11.11.1'
        classpath 'de.undercouch:gradle-download-task:5.6.0'
        classpath 'gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0'
        classpath "com.palantir.gradle.docker:gradle-docker:0.36.0"
    }
}

plugins {
    id 'org.graalvm.buildtools.native' version '0.10.5'
}

description = "WaldOT Server Application"
group = 'net.rossonet.waldot'
version = "${rootProject.version}"

ext {
    sourceCompatibility = 21
    targetCompatibility = 21
    mainClassName = "net.rossonet.agent.MainAgent"
    mainPicocliCommandClass = "net.rossonet.agent.WaldotEngine"
    debianName = "${project.name}"
    packageName = "${project.name}"
    ossrhPassword = System.getenv('OSSRH_PASSWORD')
}

repositories {
    mavenCentral()
    maven {
       url "https://repo.craftyn.com/repository/snapshots/" 
    }
    maven { url 'https://jitpack.io' }
}

apply plugin: 'com.palantir.docker'
apply plugin: 'java'
//apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: "com.gorylenko.gradle-git-properties"
apply plugin: 'jacoco'
apply plugin: "com.netflix.nebula.ospackage"
apply plugin: 'base'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'distribution'
apply plugin: 'application'

dependencies {
    implementation project(':waldot-namespace')
    implementation project(':waldot-plugin-os')
    implementation project(':waldot-plugin-bag')
    implementation project(':waldot-plugin-tinkerpop')
    implementation 'org.slf4j:jul-to-slf4j:2.0.17'
    implementation 'org.slf4j:slf4j-simple:2.0.16'
    implementation group: 'info.picocli', name: 'picocli', version: '4.7.6'

    testImplementation 'org.eclipse.digitaltwin.aas4j:aas4j-model:1.0.4'
    testImplementation group: 'org.eclipse.digitaltwin.aas4j', name: 'dataformat-aasx', version: '1.0.0-milestone-04'
    testImplementation group: 'org.eclipse.digitaltwin.aas4j', name: 'dataformat-xml', version: '1.0.0-milestone-04'
    testImplementation group: 'org.eclipse.digitaltwin.aas4j', name: 'dataformat-json', version: '1.0.0-milestone-04'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.12.0'
    testImplementation group: 'com.ginsberg', name: 'junit5-system-exit', version: '2.0.2'
    testImplementation group: 'org.apache.tinkerpop', name: 'gremlin-test', version: '3.7.3'
    testImplementation group: 'com.google.inject', name: 'guice', version: '7.0.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.12.0'
}

task eclipseClosedDependencies {}

task eclipseClosedDependencies_ {}

compileJava {
    options.encoding = "UTF-8"
    options.compilerArgs.addAll(['-XprintProcessorInfo'])
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

docker {
    name "rossonet/${project.name.replaceAll(/\B[A-Z]/) { '-' + it }.toLowerCase()}:${version}"
    dockerfile file('Dockerfile.gradle')
    files "$buildDir/distributions/${project.name}-${version}.tar"
    pull true
    noCache true
}

tasks.dockerPrepare.dependsOn("distTar")

task view { doLast { println "Working on project ${project.name} [ ${project.description} ]" } }

jar {
    manifest {
        attributes 'Main-Class': "${mainClassName}"
    }
    enabled = true
}

jar.doLast { task ->
	ant.checksum file: task.archivePath
}

shadowJar {
    minimize()
}

java {
    withJavadocJar()
    withSourcesJar()
}

gitProperties {
    failOnNoGitDirectory = false
    customProperty 'component', "${project.name}"
    dateFormat = "yyyy-MM-dd HH:mm:ssZ"
    dateFormatTimeZone = 'GMT'
}

shadowJar.doLast{ println ("You can find the fat jar of ${project.name} at $buildDir/libs/${project.name}-${version}-all.jar") }

test {
    useJUnitPlatform()
}

javadoc{
    destinationDir = file("${project.buildDir}/docs/javadoc/${project.name}/")
    failOnError=false
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

application {
    mainClass = "${mainClassName}"
    applicationDefaultJvmArgs = ["--add-opens", "java.base/java.lang=ALL-UNNAMED","--add-opens", "java.base/java.util=ALL-UNNAMED"] 
}

graalvmNative {
    toolchainDetection = false
    binaries {
        main {
            verbose = true
            sharedLibrary = false
            mainClass = "${mainClassName}"
        }
    }
}

task generateGraalReflectionConfig (dependsOn: 'classes', type: JavaExec) {
    group = "Build"
    description = "Generate GraalVM Reflection Configuration."
    main = 'picocli.codegen.aot.graalvm.ReflectionConfigGenerator'
    classpath = configurations.annotationProcessor + sourceSets.main.runtimeClasspath
    def outputFile = new File(project.buildDir, 'cli-reflect.json')
    args = ["--output=$outputFile", "${mainPicocliCommandClass}"]
}

task generateManpageAsciiDoc(type: JavaExec) {
    dependsOn(classes)
    group = "Documentation"
    description = "Generate AsciiDoc manpage."
    classpath(configurations.compileClasspath, configurations.annotationProcessor, sourceSets.main.runtimeClasspath)
    main 'picocli.codegen.docgen.manpage.ManPageGenerator'
    args "${mainPicocliCommandClass}", "--outdir=${project.buildDir}/generated-picocli-docs", "-v" //, "--template-dir=src/docs/mantemplates"
}
