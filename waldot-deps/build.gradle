buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.gorylenko.gradle-git-properties:gradle-git-properties:2.5.0"
        classpath("org.jreleaser:org.jreleaser.gradle.plugin:1.21.0")
    }
}

description = "WaldOT dependencies"
group = 'net.rossonet.waldot'
version = "${rootProject.version}"

ext {
    sourceCompatibility = 21
    targetCompatibility = 21
}

repositories {
    mavenCentral()
    maven {
       url "https://repo.craftyn.com/repository/snapshots/" 
    }
    maven { url 'https://jitpack.io' }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'distribution'
apply plugin: 'signing'
apply plugin: 'maven-publish'
apply plugin: 'org.jreleaser'

configurations.all {
  resolutionStrategy {
        //failOnVersionConflict()
        force 'org.yaml:snakeyaml:2.5'
        force 'org.slf4j:slf4j-api:2.0.17'
        //force 'org.bouncycastle:bcprov-jdk18on:1.83'
        //force 'org.bouncycastle:bcpkix-jdk18on:1.83'
        force 'commons-logging:commons-logging:1.3.2'
        eachDependency { details ->
            if (details.requested.group == 'io.netty') {
                details.useVersion '4.2.10.Final'
                details.because 'Evita conflitto tra Netty 4.1 e 4.2 (IncompatibleClassChangeError)'
            }
        }
    }
}

dependencies {
    api 'org.json:json:20250107'
    api 'org.yaml:snakeyaml:2.5'
    api 'org.apache.commons:commons-jexl3:3.4.0'
    // gestione TPM
    api("com.microsoft.azure:TSS.Java:1.0.0")
    api("org.bouncycastle:bcprov-jdk18on:1.83")
    api("org.bouncycastle:bcpkix-jdk18on:1.83")
    // libreria ICE
    //api("org.jitsi:ice4j:3.2-9-gb64c86f")
    //api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.18.3'
    //api("com.google.guava:guava:33.5.0-jre")
    // gremlin
    api group: 'org.apache.tinkerpop', name: 'gremlin-core', version: '3.7.3'
    // OPCUA
    api group: 'org.eclipse.milo', name: 'milo-sdk-server', version: '1.1.0'
}

task eclipseClosedDependencies {}

task eclipseClosedDependencies_ {}

compileJava {
    options.encoding = "UTF-8"
    options.compilerArgs.addAll(['-XprintProcessorInfo'])
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

task view { doLast { println "Working on project ${project.name} [ ${project.description} ]" } }

jar {
    enabled = true
}

jar.doLast { task ->
	ant.checksum file: task.archivePath
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

javadoc{
    destinationDir = file("${project.buildDir}/docs/javadoc/${project.name}/")
    failOnError=false
}

publishing {
  publications {
    mavenJava(MavenPublication){
      artifactId = "${project.name}"
      from components.java
      versionMapping {
        usage('java-api') {
          fromResolutionOf('runtimeClasspath')
        }
        usage('java-runtime') {
          fromResolutionResult()
        }
      }
      pom {
        name = "${project.name}"
        description = "${project.description}"
        url = 'https://www.rossonet.net/'
        licenses {
          license {
            name = 'Apache-2.0 license'
            url = 'https://www.apache.org/licenses/LICENSE-2.0'
          }
        }
        developers {
          developer {
            id = 'ar4k'
            name = 'Andrea Ambrosini'
            email = 'andrea.ambrosini@rossonet.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/rossonet/waldot.git'
          developerConnection = 'scm:git:ssh://github.com:rossonet/waldot.git'
          url = 'https://github.com/rossonet/waldot/tree/master'
        }
      }
    }
  }
  repositories {
    maven {
      name = 'staging'
      url  = layout.buildDirectory.dir('staging-deploy')
    }
  }
}

jreleaser {
  signing {
    active = 'ALWAYS'
    armored = true
    secretKey = System.getenv('GPG_PRIVATE')
    publicKey = System.getenv('GPG_PUBLIC')
  }
  deploy {
    maven {
      mavenCentral {
        sonatype {
          active = 'ALWAYS'
          url = 'https://central.sonatype.com/api/v1/publisher'
          stagingRepository(
              layout.buildDirectory
                     .dir('staging-deploy')
                     .get()
                     .asFile
                     .absolutePath
          )
          username = System.getenv('OSSRH_USERNAME')
          password = System.getenv('OSSRH_PASSWORD') 
        }
      }
    }
  }
}

tasks.named('publish').configure { dependsOn 'jreleaserDeploy' }
tasks.named('jreleaserDeploy').configure { dependsOn 'publishAllPublicationsToStagingRepository' }

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}