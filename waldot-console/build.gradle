buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.gorylenko.gradle-git-properties:gradle-git-properties:2.4.1"
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:11.9.1'
        classpath 'de.undercouch:gradle-download-task:5.6.0'
        classpath 'gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0'
        classpath "com.palantir.gradle.docker:gradle-docker:0.36.0"
    }
}

plugins {
    id 'org.graalvm.buildtools.native' version '0.10.2'
}

description = "WaldOT Terminal Console"
group = 'net.rossonet.waldot'
version = '0.1.1'

ext {
    sourceCompatibility = 21
    targetCompatibility = 21
    mainClassName = "net.rossonet.agent.MainAgent"
    mainPicocliCommandClass = "net.rossonet.agent.WaldotEngine"
    debianName = "${project.name}"
    packageName = "${project.name}"
    ossrhPassword = System.getenv('OSSRH_PASSWORD')
}

repositories {
    mavenCentral()
    maven {
       url "https://repo.craftyn.com/repository/snapshots/" 
    }
    maven { url 'https://jitpack.io' }
}

apply plugin: 'com.palantir.docker'
apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: "com.gorylenko.gradle-git-properties"
apply plugin: 'jacoco'
apply plugin: "com.netflix.nebula.ospackage"
apply plugin: 'base'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'distribution'
apply plugin: 'application'

dependencies {
    api project(':waldot-api')
    api 'org.json:json:20250107'
    api 'org.yaml:snakeyaml:2.3'
    api 'org.apache.commons:commons-jexl3:3.4.0'
    api 'org.slf4j:jul-to-slf4j:2.0.16'
    implementation 'org.slf4j:slf4j-simple:2.0.16'
    api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.18.2'
    // OPCUA
    implementation group: 'org.eclipse.milo', name: 'sdk-client', version: '0.6.15'
    implementation group: 'org.eclipse.milo', name: 'dictionary-manager', version: '0.6.15'
    implementation group: 'org.eclipse.milo', name: 'dictionary-reader', version: '0.6.15'
    implementation group: 'org.eclipse.milo', name: 'bsd-parser-core', version: '0.3.8'
    // picocli
    implementation group: 'info.picocli', name: 'picocli', version: '4.7.6'
    testImplementation project(':waldot-app')
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.4'
    testImplementation group: 'com.ginsberg', name: 'junit5-system-exit', version: '2.0.2'
    testImplementation group: 'org.apache.tinkerpop', name: 'gremlin-test', version: '3.7.3'
    testImplementation group: 'com.google.inject', name: 'guice', version: '7.0.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.0'
}

task eclipseClosedDependencies {}

task eclipseClosedDependencies_ {}

compileJava {
    options.encoding = "UTF-8"
    options.compilerArgs.addAll(['-XprintProcessorInfo'])
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

docker {
    name "rossonet/${project.name.replaceAll(/\B[A-Z]/) { '-' + it }.toLowerCase()}:${version}"
    dockerfile file('Dockerfile.gradle')
    files "$buildDir/distributions/${project.name}-${version}.tar"
    pull true
    noCache true
}

tasks.dockerPrepare.dependsOn("distTar")

task view { doLast { println "Working on project ${project.name} [ ${project.description} ]" } }

jar {
    manifest {
        attributes 'Main-Class': "${mainClassName}"
    }
    enabled = true
}

jar.doLast { task ->
	ant.checksum file: task.archivePath
}

shadowJar {
    minimize()
}

java {
    withJavadocJar()
    withSourcesJar()
}

gitProperties {
    failOnNoGitDirectory = false
    customProperty 'component', "${project.name}"
    dateFormat = "yyyy-MM-dd HH:mm:ssZ"
    dateFormatTimeZone = 'GMT'
}

shadowJar.doLast{ println ("You can find the fat jar of ${project.name} at $buildDir/libs/${project.name}-${version}-all.jar") }

test {
    useJUnitPlatform()
}

javadoc{
    destinationDir = file("${project.buildDir}/docs/javadoc/${project.name}/")
    failOnError=false
}

task theiaIdeBackend(type: Exec) {
    workingDir "./"
    commandLine 'docker', 'run', '--init', '--rm', '-p', '3000:3000', '-d', '-v', "${projectDir}:/home/project:cached", 'rossonet/theia-ide:latest'
}

task theiaIdeBackendNoCached(type: Exec) {
    workingDir "./"
    commandLine 'docker', 'run', '--init', '--rm', '-p', '3000:3000', '-d', '-v', "${projectDir}:/home/project", 'rossonet/theia-ide:latest'
}

task theiaIdeBackendNoVolume(type: Exec) {
    workingDir "./"
    commandLine 'docker', 'run', '--init', '--rm', '-p', '3000:3000', '-d', '--name', "docker-ide-${project.name}", 'rossonet/theia-ide:latest'
}

task theiaIdeBackendCopy(type: Exec) {
    theiaIdeBackendCopy.dependsOn("theiaIdeBackendNoVolume")
    workingDir "./"
    commandLine 'docker', 'cp', '.', "docker-ide-${project.name}:/home/project/"
}

task theiaIdeBackendStart(type: Exec) {
    description 'Run Theia IDE container with docker.'
    theiaIdeBackendStart.dependsOn("theiaIdeBackendCopy")
    group "Theia IDE on Docker Container"
    workingDir "./"
    commandLine 'docker', 'exec', '-u', 'root', "docker-ide-${project.name}", '/bin/chown', '-R', 'theia:theia', '/home/project'
    commandLine 'docker', 'exec', '-u', 'root', "docker-ide-${project.name}", '/bin/chown', '-R', 'theia:theia', '/home/theia'
    doLast { println "\n\n*** You can find the Theia IDE at http://localhost:3000 ***" }
    doLast { println "To shutdown the IDE:\ndocker stop docker-ide-${project.name}\n- save your work on repository before!\n\n" }
}

task printTheiaIdeBackendDockerCommand(type: Exec) {
    workingDir "./"
    commandLine 'echo', 'docker', 'run', '--init', '-p', '3000:3000', '-d', '--name', "docker-ide-${project.name}", 'rossonet/theia-ide:latest'
}

task theiaIdeLocalBrowser(type: Exec) {
    group "Theia IDE on Docker Container"
    description 'Open browser to local Theia IDE.'
    workingDir "./"
    commandLine 'xdg-open', 'http://localhost:3000'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

application {
    mainClass = "${mainClassName}"
}

graalvmNative {
    toolchainDetection = false
    binaries {
        main {
            verbose = true
            sharedLibrary = false
            mainClass = "${mainClassName}"
        }
    }
}

task generateGraalReflectionConfig (dependsOn: 'classes', type: JavaExec) {
    group = "Build"
    description = "Generate GraalVM Reflection Configuration."
    main = 'picocli.codegen.aot.graalvm.ReflectionConfigGenerator'
    classpath = configurations.annotationProcessor + sourceSets.main.runtimeClasspath
    def outputFile = new File(project.buildDir, 'cli-reflect.json')
    args = ["--output=$outputFile", "${mainPicocliCommandClass}"]
}

task generateManpageAsciiDoc(type: JavaExec) {
    dependsOn(classes)
    group = "Documentation"
    description = "Generate AsciiDoc manpage."
    classpath(configurations.compileClasspath, configurations.annotationProcessor, sourceSets.main.runtimeClasspath)
    main 'picocli.codegen.docgen.manpage.ManPageGenerator'
    args "${mainPicocliCommandClass}", "--outdir=${project.buildDir}/generated-picocli-docs", "-v" //, "--template-dir=src/docs/mantemplates"
}
